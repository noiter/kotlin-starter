buildscript {
    ext {
        kotlinVersion = "1.2.10"
        junitPlatformVersion = "1.0.0"
        spekVersion = "1.1.5"
        joobyVersion = "1.2.3"
    }

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.google.gradle:osdetector-gradle-plugin:1.4.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.3.RELEASE"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jooby:jooby-gradle-plugin:$joobyVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

apply plugin: "io.spring.dependency-management"
apply plugin: "com.google.osdetector"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "jooby"
apply plugin: "org.junit.platform.gradle.plugin"

group "starter.kotlin"
version "1.0"

// Startup class:
mainClassName = "starter.kotlin.AppKt"
sourceCompatibility = 1.8

task wrapper(type: Wrapper) {
    gradleVersion = "4.2"
}

dependencyManagement {
    imports {
        mavenBom "org.jooby:jooby-bom:$joobyVersion"
    }
}

dependencies {
    compile 'org.jooby:jooby-lang-kotlin'
    compile 'org.jooby:jooby-netty'
    compile "io.netty:netty-tcnative-boringssl-static:${dependencyManagement.importedProperties['boringssl.version']}:${osdetector.classifier}"

    testCompile "org.jetbrains.spek:spek-api:$spekVersion"
    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"
    testCompile "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    testCompile 'org.amshove.kluent:kluent:1.23'
    testCompile 'io.rest-assured:rest-assured:3.0.5'
}

/** We diverge from the default resources structure to adopt the Jooby standard: */
sourceSets.main.resources {
    srcDirs = ["conf", "public"]
}
